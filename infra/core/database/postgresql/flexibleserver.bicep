
metadata description = 'Creates an Azure Database for PostgreSQL - Flexible Server.'
param name string
param location string = resourceGroup().location
param tags object = {}

param sku object
param storage object
param administratorLogin string
@secure()
param administratorLoginPassword string
param databaseNames array = []
param allowAzureIPsFirewall bool = false
param allowAllIPsFirewall bool = false
param allowedSingleIPs array = []
param azureExtensions array = []

// PostgreSQL version
param version string

resource postgresServer 'Microsoft.DBforPostgreSQL/flexibleServers@2023-03-01-preview' = {
  location: location
  tags: tags
  name: name
  sku: sku
  properties: {
    version: version
    administratorLogin: administratorLogin
    administratorLoginPassword: administratorLoginPassword
    storage: storage
    highAvailability: {
      mode: 'Disabled'
    }

  }

  resource database 'databases' = [for name in databaseNames: {
    name: name
  }]
}

resource firewall_all 'Microsoft.DBforPostgreSQL/flexibleServers/firewallRules@2023-03-01-preview' = if (allowAllIPsFirewall) {
  name: 'allow-all-IPs'
  parent: postgresServer
  properties: {
    startIpAddress: '0.0.0.0'
    endIpAddress: '255.255.255.255'
  }
}

resource firewall_azure 'Microsoft.DBforPostgreSQL/flexibleServers/firewallRules@2023-03-01-preview' = if (allowAzureIPsFirewall) {
  name: 'allow-all-azure-internal-IPs'
  parent: postgresServer
  properties: {
    startIpAddress: '0.0.0.0'
    endIpAddress: '0.0.0.0'
  }
}

resource firewall_single 'Microsoft.DBforPostgreSQL/flexibleServers/firewallRules@2023-03-01-preview' = [for ip in allowedSingleIPs: {
  name: 'allow-single-${replace(ip, '.', '')}'
  parent: postgresServer
  properties: {
    startIpAddress: ip
    endIpAddress: ip
  }
}]

// Workaround issue https://github.com/Azure/bicep-types-az/issues/1507

resource configurations 'Microsoft.DBforPostgreSQL/flexibleServers/configurations@2023-03-01-preview' = if (length(azureExtensions) > 0) {
  name: 'azure.extensions'
  parent: postgresServer
  properties: {
    value: join(azureExtensions, ',')
    source: 'user-override'
  }
  dependsOn: [
    firewall_all, firewall_all, firewall_single
  ]
} 


output POSTGRES_DOMAIN_NAME string = postgresServer.properties.fullyQualifiedDomainName
